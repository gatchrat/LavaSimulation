// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#include "./Tools.compute"

float TimePassed;
float TargetDensity; // How thick is the Liquid
float PressureMultiplier; //How Fast should we reach the target state
float ViscosityMultiplier;
float BoundsWidth;
float BoundsHeight;
float BoundsDepth;
float MaxSpeed;
RWStructuredBuffer<float> CachedDensities;
RWStructuredBuffer<float3> PredictedPosition;

float3 CalcPosition (LavaPoint Point) {
    return Point.Position+Point.Velocity*TimePassed;
}
float GetPressureFromDensity(float Density) {
    float DensityDiff = Density-TargetDensity;
    float Pressure = DensityDiff * PressureMultiplier;
    return Pressure;
}
float CalcSharedPressure(float DensityA, float DensityB) {
    float a = GetPressureFromDensity(DensityA);
    float b = GetPressureFromDensity(DensityB);
    return(a+b)/2;
}
float3 CalcPressureForce(int PointID) {
    LavaPoint Point = Points[PointID];

    float Density = CachedDensities[PointID];
    if(Density == 0) {
        return float3(0,0,0);
    }
    float3 DirectionalPressure = 0;
    float sqrRadius = SmoothingRadius * SmoothingRadius;

    for(uint i = 0; i<ParticleCount; i++) {
        if(i == PointID) continue;
        float3 Direction = PredictedPosition[i]-PredictedPosition[PointID];
        float sqrDstToNeighbour = dot(Direction,Direction);
        float Distance = sqrt(sqrDstToNeighbour);

        if (sqrDstToNeighbour > sqrRadius) continue;

        float3 AbsDirection = Distance > 0 ? Direction / Distance : float3(0, 1,0);
        float NeighborDensity = CachedDensities[i];
        float Slope = DerivativeSpikyPow23D(Distance,SmoothingRadius);
        float SharedPressure = CalcSharedPressure(Density,NeighborDensity);
        if(NeighborDensity > 0) {
            DirectionalPressure += AbsDirection*Slope*SharedPressure/NeighborDensity;
        }
    }
    if(Density == 0) {
        return float3(0,0,0);
    }
    return float3(DirectionalPressure.x,DirectionalPressure.y,DirectionalPressure.z)/Density;
}

//Particles closeby slow me down or speed me up, kinda like friction. Known as Viscosity.
float3 CalcViscosityForce(int PointID) {
    LavaPoint Point = Points[PointID];

    float Density = CachedDensities[PointID];
    if(Density == 0) {
        return float3(0,0,0);
    }
    float3 ViscosityForce = 0;
    float sqrRadius = SmoothingRadius * SmoothingRadius;

    for(uint i = 0; i<ParticleCount; i++) {
        if(i == PointID) continue;
        float3 Direction = PredictedPosition[i]-PredictedPosition[PointID];
        float sqrDstToNeighbour = dot(Direction,Direction);
        float Distance = sqrt(sqrDstToNeighbour);

        if (sqrDstToNeighbour > sqrRadius) continue;

        float3 NeighborVelocity = Points[i].Velocity;
        float Influence = SmoothingKernelPoly63D(Distance,SmoothingRadius);
        ViscosityForce += (NeighborVelocity-Point.Velocity)*Influence;
    }
    return float3(ViscosityForce.x,ViscosityForce.y,ViscosityForce.z)*ViscosityMultiplier;
}
//TODO: Add Near Pressure to prevent:
//Density Target pulls things together -> Lava Clump gets "ripped" into smaller clumps because the big one wasnt quite dense enough
//Idea: Push close particles always a little apart to disincentivise clumping on small scales?
//INFO: Increase the force multiplier makes the simulation more accurate, but requires more fps to not break down due to too big time steps
float3 ClampVelocity(float3 Velocity) {
    float Value = dot(Velocity, Velocity);
    float Size = sqrt(Value);
    if(Size > MaxSpeed) {
        Velocity = Velocity/Size*MaxSpeed;//Scale down to 1 and then back up to Clamp Vector size to Maxspeed
    }
    return Velocity;
}

float3 CalcVelocity (int PointID) {
    LavaPoint Point = Points[PointID];
    const float3 Gravity = float3(0,-9.81,0);
    Point.Velocity+=Gravity*TimePassed;
    //F =a/m where m is the density because fluid
    Point.Velocity +=CalcPressureForce(PointID)*TimePassed;
    Point.Velocity +=CalcViscosityForce(PointID)*TimePassed;
    return ClampVelocity(Point.Velocity);
}

LavaPoint ReColor(int PointID) {
    LavaPoint Point = Points[PointID];
    Point.Color =  float4(CachedDensities[PointID],0,0,0);
    return Point;
}
LavaPoint HandleCollision(LavaPoint Point) {
    const float EnergyLossOnCollision = 0.5f;
    const float EnergyLossOnCollisionFloor = 0.7f;
    if (Point.Position.y < 0) {
        Point.Position.y = 0;
        Point.Velocity = float3(Point.Velocity.x,-Point.Velocity.y*EnergyLossOnCollisionFloor,Point.Velocity.z);
    }
    if (Point.Position.y > BoundsHeight) {
        Point.Position.y = BoundsHeight;
        Point.Velocity = float3(Point.Velocity.x,-Point.Velocity.y*EnergyLossOnCollisionFloor,Point.Velocity.z);
    }
    if (Point.Position.x < -BoundsWidth/2) {
        Point.Position.x = -BoundsWidth/2;
        Point.Velocity = float3(-Point.Velocity.x*EnergyLossOnCollision,Point.Velocity.y,Point.Velocity.z);
    }
    if (Point.Position.x > BoundsWidth/2) {
        Point.Position.x = BoundsWidth/2;
        Point.Velocity = float3(-Point.Velocity.x*EnergyLossOnCollision,Point.Velocity.y,Point.Velocity.z);
    }
    if (Point.Position.z < -BoundsDepth/2) {
        Point.Position.z = -BoundsDepth/2;
        Point.Velocity = float3(Point.Velocity.x,Point.Velocity.y,-Point.Velocity.z*EnergyLossOnCollision);
    }
    if (Point.Position.z > BoundsDepth/2) {
        Point.Position.z = BoundsDepth/2;
        Point.Velocity = float3(Point.Velocity.x,Point.Velocity.y,-Point.Velocity.z*EnergyLossOnCollision);
    }
    return Point;
}

[numthreads(10,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
    LavaPoint CurPoint = Points[id.x];
    CurPoint = ReColor(id.x);
    CurPoint.Velocity = CalcVelocity(id.x);
    CurPoint.Position = CalcPosition(CurPoint);
    Points[id.x] = HandleCollision(CurPoint);
}